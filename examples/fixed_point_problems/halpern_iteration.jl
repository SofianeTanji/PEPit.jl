using PEPit
using OrderedCollections

function wc_halpern_iteration(n; verbose=true)
    problem = PEP()

    param = OrderedDict("L" => 1.0)
    A = declare_function!(problem, LipschitzOperator, param; reuse_gradient=true)

    xs, _, _ = fixed_point!(A)
    x0 = set_initial_point!(problem)

    set_initial_condition!(problem, (x0 - xs)^2 <= 1)

    x = x0
    for i in 0:(n - 1)
        x = 1 / (i + 2) * x0 + (1 - 1 / (i + 2)) * gradient!(A, x)
    end

    set_performance_metric!(problem, (x - gradient!(A, x))^2)

    pepit_tau = solve!(problem; verbose=verbose)
    theoretical_tau = (2 / (n + 1))^2

    if verbose
        println("*** Example file: worst-case performance of Halpern Iterations ***")
        println("\tPEPit guarantee:\t ||xN - AxN||^2 <= $(round(pepit_tau, digits=8)) ||x0 - x_*||^2")
        println("\tTheoretical guarantee:\t ||xN - AxN||^2 <= $(round(theoretical_tau, digits=8)) ||x0 - x_*||^2")
    end

    return pepit_tau, theoretical_tau
end

pepit_tau, theoretical_tau = wc_halpern_iteration(25; verbose=true)


